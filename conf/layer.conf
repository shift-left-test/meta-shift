# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILES_DYNAMIC += " \
    clang-layer:${LAYERDIR}/dynamic-layers/meta-clang/recipes-*/*/*.bb \
    clang-layer:${LAYERDIR}/dynamic-layers/meta-clang/recipes-*/*/*.bbappend \
    clang-layer:${LAYERDIR}/dynamic-layers/meta/recipes-*/*/*.bb \
    clang-layer:${LAYERDIR}/dynamic-layers/meta/recipes-*/*/*.bbappend \
"

BBFILE_COLLECTIONS += "meta-shift"
BBFILE_PATTERN_meta-shift = "^${LAYERDIR}/"
BBFILE_PRIORITY_meta-shift = "17"

LAYERDEPENDS_meta-shift = "core meta-python openembedded-layer"
LAYERSERIES_COMPAT_meta-shift = "zeus"

INHERIT += "shifttasks"

# List of required host tools
HOSTTOOLS += "timeout"

# List of tools for the do_checkcode task
# Currently supported tools are: cpplint, cppcheck, clang-tidy (requires meta-clang)
SHIFT_CHECKCODE_TOOLS ??= "cpplint cppcheck ${@bb.utils.contains('BBFILE_COLLECTIONS', 'clang-layer', 'clang-tidy', '', d)}"

# exclude file or path for checkcode (relative path starting from the source directory)
SHIFT_CHECKCODE_EXCLUDES ??= ""

# Default value for the number of maximum mutants
SHIFT_CHECKTEST_LIMIT ??= "20"

# All of the source files are considered for mutation by default
# Options for the target scope of mutation: all, commit
SHIFT_CHECKTEST_SCOPE ??= "all"

# Default mutation generator
# Currently supported generators are: random, uniform, weighted
SHIFT_CHECKTEST_GENERATOR ??= "uniform"

# Silence the test output while running the do_checktest task
SHIFT_CHECKTEST_VERBOSE ??= "0"

SHIFT_CHECKTEST_EXTENSIONS ??= ""
SHIFT_CHECKTEST_EXCLUDES ??= ""

# Random seed for the mutation generator
SHIFT_CHECKTEST_SEED ??= ""

# exclude file or path for coverage (relative path starting from the source directory)
SHIFT_COVERAGE_EXCLUDES ??= ""

# Do not synchronize the tasks by default
SHIFT_PARALLEL_TASKS ??= "1"
